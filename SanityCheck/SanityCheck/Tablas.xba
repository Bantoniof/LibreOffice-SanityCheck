<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE script:module PUBLIC "-//OpenOffice.org//DTD OfficeDocument 1.0//EN" "module.dtd">
<script:module xmlns:script="http://openoffice.org/2000/script" script:name="Tablas" script:language="StarBasic"> Rem  *****  BASIC   Módulo Tablas *****
 Rem Este módulo forma parte de la extensión SanityCheck
 Rem Autor: bantoniof@libreoffice.org
 Rem Licencia GNU v3 o posterior
  
 Option Explicit
 Dim oTablas As Object
 Dim oTabla As Object
 Dim vCursor As Object
 Dim oDlgTablas As Object
&apos; Dim Dlg_Activo as Boolean &apos;dlg No modal evita ejecuciones múltiples - declaración en imgs
&apos; Dim Dlg_Mostrado as Boolean &apos; dlg No modal para cerrar el diálogo   - declaración en imgs
 Dim Indice_Tbl As Integer
 Dim Ultima_Tbl As Integer

 Rem ### MENU #########
Sub Dlg_Tablas&apos;(oTabla)
   Call L10n.Trad_Tablas
 If _Aux.F_Guardado = 0 Then : Exit Sub : End If   
 If _Aux.F_Prueba_Sel = 0 Then :Exit Sub : End If
   Call _Aux.DirectorioPlantillas &apos;comprueba actualización de plantilla
   Dlg_Mostrado = False
   DLg_Activo = False
   oTablas = ThisComponent.TextTables
   Ultima_Tbl = oTablas.getCount -1
 If Ultima_Tbl &lt; 0 Then : MsgBox (T_Txt(14), 0+48+0, T_Txt(0) &amp; Extension) : Exit Sub  : End If &apos; no hay tablas en el documento
   Indice_Tbl = 0
   oTabla = oTablas.getbyIndex(Indice_Tbl)
   vCursor = ThisComponent.CurrentController.getViewCursor()
   vcursor.jumptoFirstPage(False)
   vcursor.gotoRange(oTabla.getAnchor,False)
   If Dlg_Activo = False Then
     DialogLibraries.loadLibrary(&quot;SanityCheck&quot;)
     oDlgTablas = CreateUNODialog(DialogLibraries.getByName(&quot;SanityCheck&quot;).getByName(&quot;DlgTablas&quot;))
     If getGUIType &gt; 1 then oDlgTablas.getPeer().setProperty( &quot;NativeWidgetLook&quot;, False ) 
     Dlg_Activo = True
     Dlg_Mostrado = True
     With oDlgTablas.Model
     .Title =  T_Txt(0) &amp; Extension
     .PositionX = 530
     .PositionY = 60
     .Step = 1
     End With
     Call Parametros_Tablas
     Do While Dlg_Mostrado = True
       Wait 200
       oDlgTablas.SetVisible(True)
     Loop
     oDlgTablas.dispose
     Dlg_Activo = False
   End If
End Sub

 Rem ############
Sub parar
 Rem solo para bloqueos del diálogo durante la programación
   Dlg_Mostrado = False
   DLg_Activo = False
End Sub

 Rem ############
Sub Parametros_Tablas
   If _Aux.F_Guardado = 0 Then : Exit Sub : End If
   If _Aux.F_Prueba_Sel = 0 Then : Exit Sub : End If

   Ultima_Tbl = oTablas.getCount -1

   If Ultima_Tbl &lt; 0 Then : Dlg_Mostrado = False : MsgBox (T_Txt(17), 0+48+0, T_Txt(0) &amp; Extension) : Exit Sub : End If   &apos;si se borran todas las tablas

   oTabla = oTablas.getbyIndex(Indice_Tbl)
   vCursor.gotoRange(oTabla.Anchor,False)
   With oDlgTablas.getModel
     .getByName(&quot;Lbl_NombreT&quot;).Label = T_Txt(1)
     .getByName(&quot;Lbl_TablaNum&quot;).Label = T_Txt(16) &amp; Indice_Tbl +1 &amp; &quot; / &quot;  &amp; Ultima_Tbl +1
     .getByName(&quot;Lbl_EstiloT&quot;).Label = T_Txt(2)
     .getByName(&quot;Fr_Tipo&quot;).Label = T_Txt(3)
     .getByName(&quot;Opt_Datos&quot;).Label = T_Txt(4)
     .getByName(&quot;Opt_Posicion&quot;).Label = T_Txt(5)
     .getByName(&quot;Lbl_Info&quot;).Label = T_Txt(6)
     .getByName(&quot;Btn_Anterior&quot;).Label = T_Txt(7)
     .getByName(&quot;Btn_Optimizar&quot;).Label = T_Txt(8)
     .getByName(&quot;Btn_Siguiente&quot;).Label = T_Txt(10)
     .getByName(&quot;Btn_Salir&quot;).Label = T_Txt(11)
     .getByName(&quot;Lbl_Nombre&quot;).Label = oTabla.Name 
     .getByName(&quot;Lbl_Estilo&quot;).Label = oTabla.TableTemplateName
     .getbyName(&quot;Btn_Optimizar&quot;).EnableVisible = True
     .getbyName(&quot;Lbl_Optimizada&quot;).EnableVisible= False
     .getbyName(&quot;Opt_Datos&quot;).State= 0
     .getbyName(&quot;Opt_Posicion&quot;).State= 0


     If Indice_Tbl = 0 Then
       .getByName(&quot;Btn_Anterior&quot;).EnableVisible = False
       .getByName(&quot;Btn_Siguiente&quot;).EnableVisible = True
       With .getByName(&quot;Lbl_Ultima&quot;)
         .EnableVisible = True : .label = T_Txt(13)
         .PositionX = 10 : .PositionY = 96
       End With
       If ultima_Tbl = 0 then .getByName(&quot;Btn_Siguiente&quot;).EnableVisible = False
       
&apos;print ultima_Tbl &amp; &quot;   &quot;  &amp; indice_Tbl

     ElseIf Indice_Tbl = Ultima_Tbl Then
  
       .getByName(&quot;Btn_Siguiente&quot;).EnableVisible = False
       .getByName(&quot;Btn_Anterior&quot;).EnableVisible = True
       .getByName(&quot;Btn_Salir&quot;).state = 1
 
       With .getByName(&quot;Lbl_Ultima&quot;)
         .EnableVisible = True : .label = T_Txt(12)
         .PositionX = 60 : .PositionY = 96
       End With

     Else
       .getByName(&quot;Lbl_Ultima&quot;).EnableVisible = False
       .getByName(&quot;Btn_Siguiente&quot;).EnableVisible = True
       .getByName(&quot;Btn_Anterior&quot;).EnableVisible = True
       .getByName(&quot;Btn_Salir&quot;).state = 0
     End If
   End With
End Sub

 Rem ############
Sub OnBtn_Anterior(oEv)
   oDlgTablas = oEv.Source.Context
   If oTablas.getCount -1 &lt;&gt; Ultima_Tbl Then
     Ultima_Tbl = oTablas.getCount -1
     MsgBox (T_Txt(18), 0+64+0, T_Txt(0) &amp; Extension )
     Indice_Tbl = 0
   ElseIf Not Indice_Tbl &lt; 0 Then
     Indice_Tbl = Indice_Tbl -1
   End If
   Call Parametros_Tablas
End Sub

 Rem ############
Sub OnBtn_Siguiente(oEv)
   oDlgTablas = oEv.Source.Context
   If  oTablas.getCount -1 &lt;&gt; Ultima_Tbl Then
     Ultima_Tbl = oTablas.getCount -1
     Indice_Tbl = 0
     MsgBox (T_Txt(18), 0+64+0,T_Txt(0) &amp; Extension )
   ElseIf Indice_Tbl &lt; Ultima_Tbl Then
     Indice_Tbl = Indice_Tbl + 1
   End If
   Call Parametros_Tablas
End Sub

 Rem ############
Sub OnBtn_Salir(oEv)
   Dlg_Mostrado = False
   MsgBox (T_Txt(19), 0+64+0, T_Txt(0) &amp; Extension)
End Sub

 Rem ############
Sub OnRadioButtons(oEv)
   oDlgTablas = oEv.Source.Context
   If oDlgTablas.getModel.getByName(&quot;Opt_Datos&quot;).state = 1 Then
     oDlgTablas.getModel.getByName(&quot;Lbl_Info&quot;).label = T_Txt(20) &amp; T_Txt(22)
   Else
     oDlgTablas.getModel.getByName(&quot;Lbl_Info&quot;).label = T_Txt(21) &amp; T_Txt(22)
   End If
End Sub

 Rem ############
Sub OnBtn_Optimizar(oEv)
  oDlgTablas = oEv.Source.Context
   If oDlgTablas.getModel.getByName(&quot;Opt_Datos&quot;).state = 1 Then
     Call optimizar(oTabla,&quot;Datos&quot;)
   ElseIf oDlgTablas.getModel.getByName(&quot;Opt_Posicion&quot;).state = 1 Then
     Call optimizar(oTabla,&quot;Contenido&quot;)
   Else
    MsgBox (T_Txt(15), 0+64+0, T_Txt(0) &amp; Extension)
   End If
   oDlgTablas.getModel.getByName(&quot;Lbl_Optimizada&quot;).label = T_Txt(9)
   oDlgTablas.getModel.getbyName(&quot;Lbl_Estilo&quot;).Label = oTabla.TableTemplateName  
End Sub


 Rem ############
Sub Optimizar(oTabla As Object ,Tipo As String)

   If ThisComponent.TextTables.getCount = 0 then
     Msgbox (T_Txt(17),0+64+0, Extension)
     Dlg_Mostrado = False
     Exit sub
   End If

 Rem Propiedades comunes
   With oTabla
     .TableTemplateName = &quot;&quot;
     .CollapsingBorders = True &apos;fusionar lineas bordes
     .BreakType = 0&apos; No saltos de página
     .Split = True &apos;Quebrar tabla
     .TopMargin = 0
     .BottomMargin = 210
   End With

 Rem Necesario para cada fila
 Dim i As Integer, oRows As Object, oRow As Object
   For i= 0 To oTabla.Rows().count -1
     oRow = oTabla.getRows().getbyIndex(i)
     oRow.IsSplitAllowed = false
     oRow.IsAutoHeight= True
     oRow.Height = 0
   Next

 Rem Tabla Contenido
 &apos;La tabla debe estar centrada. Para evitar prob calculo su ancho en %. 
   If Tipo = &quot;Contenido&quot; Then  
     If otabla.HoriOrient &lt;&gt; 2 then
       Dim ancho : ancho = oTabla.width
       if ancho &gt; 16600 then&apos; la tabla se ha creado sin  indicar anchura, (devuelve una anchura mayor que el espacio)
         ancho = 100
       else 
         ancho = ancho / 166 &apos; *100 /16600 anchura pag sin margenes
       end If
       oTabla.HoriOrient = 2
       otabla.RelativeWidth = ancho
     End If 

 Rem Tabla de datos
   ElseIf Tipo = &quot;Datos&quot; Then

   Dim nRow As Integer, nCol As Integer, oCell As Object
     With oTabla
       .HoriOrient = 2  &apos;centrada horizontal  
       .RelativeWidth = 90
       .RepeatHeadline = True
       .HeaderRowCount = 1
     End With&apos; 

&apos; Estilos de párrafo y limpieza color celdas
     For nRow = 0 To oTabla.getRows().getCount() - 1
       For nCol = 0 To oTabla.getColumns().getCount() - 1
         oCell = oTabla.getCellByPosition(nCol, nRow)
         oCell.BackColor = -1 &apos; limpio color celdas
         If nRow = 0 Then
           Formato_Par(oCell.getText(), &quot;Table Heading&quot;) &apos;limpia formato directo y aplica estilo
         Else
           Formato_Par(oCell.getText(), &quot;Table Contents&quot;)
         End If
       Next
     Next 

&apos; Color filas
     For i= 0 To oTabla.Rows().count -1
       oRow = oTabla.getRows().getbyIndex(i)
       If i = 0 then
         oRow.backColor = RGB(208,208,208) &apos;Aplico color a fila
       Else
         oRow.backColor = -1
       End If  
     Next

     Lineas (oTabla,2) &apos;lineas para los bordes
     Distancias (oTabla, 100) &apos;distancias al borde (celdas)&apos;antes común
     BorraEstiloTabla (&quot;LO User Guides&quot;) 
     BorraEstiloTabla (&quot;LO_USERGUIDES&quot;) 
   End If     

   With oDlgTablas.getModel
    .getbyname(&quot;Btn_Optimizar&quot;).enableVisible = False
    .getbyName(&quot;Lbl_Optimizada&quot;).EnableVisible= True
    .getbyName(&quot;Opt_Datos&quot;).State= 0
    .getbyName(&quot;Opt_Posicion&quot;).State= 0
   End With
End Sub

 Rem ############
 &apos; Alineacion vertical centrada, aplica estilo de párrafo y limpia formato directo.
Sub Formato_Par(oText, sParStyle As String)
 Dim oEnum, oPar, tCursor
   oEnum = oText.createEnumeration()
   Do While oEnum.hasMoreElements()
     oPar = oEnum.nextElement()
     If oPar.supportsService(&quot;com.sun.star.text.Paragraph&quot;) Then
       oPar.ParaVertAlignment = 3
       oPar.ParaStyleName = sParStyle
       tCursor = oPar.Text.CreateTextCursor&apos; limpiar formato directo
       tCursor.gotoStart(false) : tCursor.gotoEnd(True)
       tcursor.setAllPropertiestodefault
     End If
   Loop
End Sub

 Rem ############
Sub Distancias (oTabla As Object, dist as integer, Optional Top as Integer) 
&apos; Notas. primero dato, despues validar
&apos; Aumento el top porque no se centra vertical bien. posible bug?
 
 If isMissing(Top) then Top = dist
 Dim oBorderDistances As New com.sun.star.table.TableBorderDistances
   With oBorderDistances
     .TopDistance = Top 
     .IsTopDistanceValid = True
     .BottomDistance = dist     
     .IsBottomDistanceValid = True
     .LeftDistance = dist 
     .IsLeftDistanceValid = True
     .RightDistance = dist  
     .IsRightDistanceValid = True
   End With
     oTabla.TableBorderDistances = oBorderDistances
End Sub


 Rem ############
Sub Lineas (oTabla As Object, lin As Integer)

 Dim x &apos;represents each BorderLine
 Dim v &apos;represents the TableBorder Object as a whole
   v = oTabla.TableBorder
   x = v.HorizontalLine : x.OuterLineWidth = lin : v.HorizontalLine = x
   x = v.TopLine : x.OuterLineWidth = lin : v.TopLine = x
   x = v.BottomLine : x.OuterLineWidth = lin : v.BottomLine = x
   x = v.VerticalLine : x.OuterLineWidth = lin : v.VerticalLine = x
   x = v.RightLine : x.OuterLineWidth = lin : v.RightLine = x
   x = v.LeftLine : x.OuterLineWidth = lin : v.LeftLine = x
   oTabla.TableBorder = v
End Sub

 Rem ############
Sub BorraEstiloTabla(sEstilo as String)

   If ThisComponent.StyleFamilies.TableStyles.HasbyName(sEstilo) Then
     Dim oEstilo : oEstilo = ThisComponent.StyleFamilies.TableStyles.getbyName(sEstilo)
     If oEstilo.isUserDefined And Not oEstilo.isInUse Then
       ThisComponent.StyleFamilies.TableStyles.removeByName(sEstilo) &apos;&quot;Estilo borrado&quot;
     End If
   End If
End sub 

 Rem ================= Fin Tablas ===========================================================
</script:module>